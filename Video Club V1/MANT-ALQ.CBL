	IDENTIFICATION DIVISION.
	 PROGRAM-ID. ALTAS-ALQUILERES.
	ENVIRONMENT DIVISION.
	 INPUT-OUTPUT SECTION.
	 FILE-CONTROL.
	      SELECT OPTIONAL ALQUILER ASSIGN TO DISK "ALQUILER.DAT"
	      ORGANIZATION IS INDEXED
	      ACCESS MODE IS DYNAMIC
	      RECORD KEY IS N-SALIDA
	      ALTERNATE RECORD KEY IS CODI-SOCIO
	      ALTERNATE RECORD KEY IS CODI-PELI
	      FILE STATUS IS ESTADO-ALQUILER.

	      SELECT OPTIONAL PELICULA ASSIGN TO DISK "PELICULA.DAT"
	      ORGANIZATION IS INDEXED
	      ACCESS MODE IS DYNAMIC
	      RECORD KEY IS COD-PELI
	      ALTERNATE RECORD KEY IS TITULO
	      ALTERNATE RECORD KEY IS GENERO WITH DUPLICATES
	      ALTERNATE RECORD KEY IS DIRECTOR WITH DUPLICATES
	      FILE STATUS IS ESTADO-PELICULA.
	
	     SELECT OPTIONAL SOCIOS ASSIGN TO DISK "SOCIOS.DAT"
	     ORGANIZATION IS INDEXED
	     ACCESS MODE IS DYNAMIC
	     RECORD KEY IS COD-SOCIO
	     ALTERNATE RECORD KEY IS NOMBRE WITH DUPLICATES
	     ALTERNATE RECORD KEY IS DNI
	     FILE STATUS IS ESTADO-SOCIOS.
	

	DATA DIVISION.
	 FILE SECTION.
	  FD ALQUILER LABEL RECORD STANDARD.
	  01 REG-ALQUILER.
	   02 N-SALIDA PIC X(8).
	   02 FECHA-ALQ.
	    03 DIA PIC 99.
	    03 MES PIC 99.
	    03 ANO PIC 9999.
	   02 CODI-SOCIO PIC X(5).
	   02 CODI-PELI PIC X(6).
	   02 PRECI-ALQ PIC 9(4).
	   02 DEVUELTA PIC X.

	  FD PELICULA LABEL RECORD STANDARD.
	  01 REG-PELICULA.
	   02 COD-PELI PIC X(6).
	   02 TITULO PIC X(30).
	   02 GENERO PIC X(15).
	   02 DIRECTOR PIC X(25).
	   02 PRECIO PIC 9(4).
	   02 CLASE-PELICULA PIC 9.
	   02 DISPONIBLE PIC X.
	   
	 FD SOCIOS LABEL RECORD IS STANDARD.
	 01 REG-SOCIOS.
	  02 COD-SOCIO PIC X(5).
	  02 NOMBRE.
	   03 APE1 PIC X(12).
	   03 APE2 PIC X(12).
	   03 NOMB PIC X(12).
	  02 DIRECCION PIC X(30).
	  02 DNI PIC X(10).
	  02 TELEFONO PIC X(10).

	 
	 WORKING-STORAGE SECTION.
	  77 ESTADO-ALQUILER PIC XX.
	  77 ESTADO-PELICULA PIC XX.
	  77 ESTADO-SOCIOS PIC XX.
	  77 EXISTE-SOCIO PIC X.
	  77 EXISTE-PELICULA PIC X.
	  77 RES PIC X.
	   88 CORRECTOS VALUE 'S' 's' 'N' 'n'.
	  77 TECLA PIC 99.
	   88 ESC VALUE 27.
	  77 ESPERA PIC X.
	  77 PRESTADO PIC X.
	
	SCREEN SECTION.
	 01 AZUL.
	  02 BACKGROUND BLUE.
	  02 FOREGROUND WHITE.
	  
	 01 NEGRO.
	  02 BACKGROUND BLACK.
	 
	 01 ROJO. 
	  02 FOREGROUND RED.
	 
	PROCEDURE DIVISION.
	 INICIO.
	     MOVE 00 TO TECLA.
	     DISPLAY NEGRO.
	     DISPLAY SPACES ERASE.
	     PERFORM ABRIR-FICHEROS.
	     DISPLAY AZUL.
	     PERFORM PANTALLA.
	     PERFORM UNTIL ESC   
		MOVE ' ' TO N-SALIDA
		PERFORM UNTIL ( N-SALIDA NOT EQUAL ' ' ) OR ( ESC )
		   ACCEPT N-SALIDA NO BEEP POSITION 39 LINE 5
		   ACCEPT TECLA FROM ESCAPE KEY
		END-PERFORM
		IF NOT ESC
		   READ ALQUILER KEY N-SALIDA
			INVALID KEY MOVE 'N' TO PRESTADO
			NOT INVALID KEY MOVE 'S' TO PRESTADO
		   END-READ
		   IF PRESTADO = 'S'                                         * PRESTADO SI
		      DISPLAY ROJO
		      DISPLAY "PELICULA PRESTADA ACTUALMENTE"
			POSITION 25 LINE 23 BLINK
		      DISPLAY AZUL
		      PERFORM PONE-DATOS
		      DISPLAY "DESEA DEVOLVER PELICULA (S/N)?"
			POSITION 25 LINE 15
		      PERFORM PIDE-RESPUESTA
		      IF RES = 'S' OR RES = 's'
			 START ALQUILER KEY = N-SALIDA
			 DELETE ALQUILER
			 MOVE CODI-PELI TO COD-PELI
			 READ PELICULA KEY IS COD-PELI
			 MOVE 'S' TO DISPONIBLE
			 REWRITE REG-PELICULA
		      END-IF
		   ELSE                                                      *PRESTADO NO
		      PERFORM PIDE-CODIGO-SOCIO
		      IF EXISTE-SOCIO = 'S'                                     * EXISTE SOCIO
			 MOVE COD-SOCIO TO CODI-SOCIO
			 PERFORM PIDE-CODIGO-PELICULA
			 IF EXISTE-PELICULA = 'S'                                   * EXISTE PELICULA
			   MOVE COD-PELI TO CODI-PELI
			   PERFORM PIDE-PRECIO-Y-FECHA
			    DISPLAY "DESEA PRESTAR PELICULA (S/N)?"
			      POSITION 25 LINE 15
			    PERFORM PIDE-RESPUESTA
			    IF RES = 'S' OR RES = 's'
			       MOVE 'N' TO DISPONIBLE
			       REWRITE REG-PELICULA
			       MOVE 'N' TO DEVUELTA
			       WRITE REG-ALQUILER
			    END-IF
			 ELSE                                                       * (NO)EXISTE PELICULA
			    PERFORM NO-HAY-PELICULA
			 END-IF                                                     * FIN EXISTE PELICULA
		      ELSE                                                      * (NO)EXISTE SOCIO
			 PERFORM NO-HAY-SOCIO
		      END-IF                                                    * FIN EXISTE SOCIO
		   
		   END-IF                                                         * FIN PRESTADO SI-NO
		END-IF
		MOVE 27 TO TECLA
	     END-PERFORM.
	     PERFORM CERRAR-FICHEROS.
	     EXIT PROGRAM.
	 
	PANTALLA.
		CALL "S-WINDOW" USING 20 60 03 17
		CANCEL "S-WINDOW"
		DISPLAY " ALQUILERES " POSITION 36 LINE 3 REVERSE
		DISPLAY "N§ de salida: ________" POSITION 25 LINE 5
		DISPLAY "C¢digo de socio: _____" POSITION 25 LINE 7
		DISPLAY "C¢digo de pel¡cula: _____" 
			POSITION 25 LINE 9
		DISPLAY "Fecha de alquiler: __/__/____" 
			POSITION 25 LINE 11
		DISPLAY "Precio de Alquiler: ____" 
			POSITION 25 LINE 13
		CALL "S-WINDOW" USING 02 77 21 24
		CANCEL "S-WINDOW"
		DISPLAY " AYUDA " POSITION 36 LINE 21 REVERSE
		DISPLAY "Introduzca el c¢digo de la pel¡cula"
			POSITION 14 LINE 22
		DISPLAY "a PRESTAR o DEVOLVER"
			POSITION 50 LINE 22.

	NO-HAY-SOCIO.
		DISPLAY 'NO EXISTE EL SOCIO'
			POSITION 30 LINE 16 BLINK
		ACCEPT ESPERA NO BEEP.
	 
	NO-HAY-PELICULA.
		DISPLAY 'NO EXISTE LA PELICULA'
			POSITION 30 LINE 16 BLINK
		ACCEPT ESPERA NO BEEP.
	 
	ABRIR-FICHEROS.
		OPEN I-O ALQUILER
		OPEN I-O SOCIOS
		OPEN I-O PELICULA.

	CERRAR-FICHEROS.
		CLOSE ALQUILER
		CLOSE SOCIOS
		CLOSE PELICULA.

	PIDE-PRECIO-Y-FECHA.
		ACCEPT DIA NO BEEP POSITION 44 LINE 11
		ACCEPT MES NO BEEP POSITION 47 LINE 11
		ACCEPT ANO NO BEEP POSITION 50 LINE 11
		ACCEPT PRECI-ALQ NO BEEP POSITION 45 LINE 13.

	PIDE-CODIGO-SOCIO.
		MOVE ' ' TO COD-SOCIO
		PERFORM UNTIL COD-SOCIO NOT EQUAL ' '
			ACCEPT COD-SOCIO NO BEEP POSITION 42 LINE 7
		END-PERFORM
		READ SOCIOS KEY IS COD-SOCIO
			INVALID KEY MOVE 'N' TO EXISTE-SOCIO
			NOT INVALID KEY MOVE 'S' TO EXISTE-SOCIO
		END-READ.

	PIDE-CODIGO-PELICULA.
		MOVE ' ' TO COD-PELI
		PERFORM UNTIL COD-PELI NOT EQUAL ' '
			ACCEPT COD-PELI NO BEEP POSITION 45 LINE 9
		END-PERFORM
		READ PELICULA KEY IS COD-PELI
			INVALID KEY MOVE 'N' TO EXISTE-PELICULA
			NOT INVALID KEY MOVE 'S' TO EXISTE-PELICULA
		END-READ.


	PONE-DATOS.
		DISPLAY N-SALIDA POSITION 39 LINE 5
		DISPLAY CODI-SOCIO POSITION 42 LINE 7
		DISPLAY CODI-PELI POSITION 45 LINE 9
		DISPLAY DIA POSITION 44 LINE 11
		DISPLAY MES POSITION 47 LINE 11
		DISPLAY ANO POSITION 50 LINE 11
		DISPLAY PRECI-ALQ POSITION 45 LINE 13.
	 
	PIDE-RESPUESTA.
		MOVE ' ' TO RES
		PERFORM UNTIL RES = 'S' OR RES = 's' 
			OR RES = 'N' OR RES = 'n'
			ACCEPT RES NO BEEP POSITION 56 LINE 15
		END-PERFORM.

